//@version=6
indicator('BTC AHR999 Index', overlay = false)

// ahr999 = (（比特币价格/200日定投成本) * （比特币价格/指数增长估值）
// 其中，按照原作者对该指标的详细说明中可知：
// • 指数增长估值= 10^[5.84 * log(币龄) - 17.01]
// • 币龄 = 当前日期距离2009年1月3日的天数
// • 200日定投成本 使用几何平均值

// 确保在日线图表上使用
if not timeframe.isdaily
    runtime.error('请在日线图表上使用此指标')

// 计算币龄（从2009年1月3日开始）
GENESIS_TIMESTAMP = timestamp('2009-01-03')
days_since_genesis = (time - GENESIS_TIMESTAMP) / (1000 * 60 * 60 * 24)

// 计算指数增长估值
// formula: 10^(5.84 * log(币龄) - 17.01)
exp_growth = math.pow(10, 5.84 * math.log10(days_since_genesis) - 17.01)

// 计算200日定投成本（简单移动平均）
// sma_200 = ta.sma(close, 200)
f_gma(src, len) =>
    sum_logs = 0.0
    for i = 0 to len - 1 by 1
        sum_logs := sum_logs + math.log(src[i])
        sum_logs
    math.exp(sum_logs / len)

length = input.int(200, 'Length', minval = 1)
gma_200 = f_gma(close, length)

// 计算 AHR999 指数
// formula: (比特币价格/200日定投成本) * (比特币价格/指数增长估值)
ahr999 = close / gma_200 * (close / exp_growth)

// 为了使所有指标在同一个视图中可见，进行数值缩放
scale_factor = 1000 // 可以根据需要调整这个缩放因子
scaled_close = close / scale_factor
scaled_gma_200 = gma_200 / scale_factor
scaled_exp_growth = exp_growth / scale_factor

// 绘制所有指标
plot(ahr999, 'AHR999', color = color.blue, linewidth = 2, precision=3)
plot(scaled_close, '当日收盘价', color = color.green, linewidth = 1, precision=3)
plot(scaled_gma_200, '200日定投成本', color = color.orange, linewidth = 1, precision=3)
plot(scaled_exp_growth, '指数增长估值', color = color.purple, linewidth = 1, precision=3)

// 添加 AHR999 的参考线
hline(0.45, '极度抄底区', color = color.green, linestyle = hline.style_dashed)
hline(1.2, '中性', color = color.gray)
hline(4, '极度卖出区', color = color.red, linestyle = hline.style_dashed)

// 添加图例说明
var tbl = table.new(position.top_right, 4, 2, bgcolor = color.new(color.black, 70))
table.cell(tbl, 0, 0, '指标', text_color = color.white)
table.cell(tbl, 0, 1, '实际值', text_color = color.white)
table.cell(tbl, 1, 0, '收盘价', text_color = color.green)
table.cell(tbl, 1, 1, str.tostring(close, format.volume), text_color = color.green)
table.cell(tbl, 2, 0, '200MA', text_color = color.orange)
table.cell(tbl, 2, 1, str.tostring(gma_200, format.volume), text_color = color.orange)
table.cell(tbl, 3, 0, '指数估值', text_color = color.purple)
table.cell(tbl, 3, 1, str.tostring(exp_growth, format.volume), text_color = color.purple)
